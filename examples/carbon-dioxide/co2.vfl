
# load the input dataset.
dat = data(file: 'co2.dat');

# shift the data locations for easier inference.
for i in std.range(n: dat.N) {
  dat[i][0] = dat[i][0] - 1995;
}

# create a model without an explicit linear trend.
mdlA = vfr(
  alpha0: 100,
  beta0: 1,
  nu: 1e-6,
  data: dat,
  factors: [cosine(mu: 0,    tau: 1e5),
            cosine(mu: 1e-3, tau: 1e4),
            cosine(mu: 1e-2, tau: 100),
            cosine(mu: 1e-1, tau: 100),
            cosine(mu: 5,    tau: 1),
            cosine(mu: 10,   tau: 1)]
);

# create a model with an explicit linear trend.
mdlB = vfr(
  alpha0: 100,
  beta0: 1,
  nu: 1e-6,
  data: dat,
  factors: [polynomial(order: 1),
            cosine(mu: 1e-3, tau: 1e4),
            cosine(mu: 1e-2, tau: 100),
            cosine(mu: 1e-1, tau: 100),
            cosine(mu: 5,    tau: 1),
            cosine(mu: 10,   tau: 1)]
);

# create a full-gradient optimizer.
opt = fg(
  maxIters: 200,
  lipschitzInit: 1e-4
);

# optimize the first model.
opt.model = mdlA;
opt.execute();

# optimize the second model.
opt.model = mdlB;
opt.execute();

# build gridded datasets for prediction.
G = [[-25, 1e-2, 75]];
meanA = data(grid: G);
meanB = data(grid: G);
varA = data(grid: G);
varB = data(grid: G);

# compute the model predictions.
mdlA.predict(mean: meanA, var: varA);
mdlB.predict(mean: meanB, var: varB);

# shift the predictions back to the original data locations.
for i in std.range(n: meanA.N) {
  meanA[i][0] = meanA[i][0] + 1995;
  meanB[i][0] = meanB[i][0] + 1995;
  varA[i][0] = varA[i][0] + 1995;
  varB[i][0] = varB[i][0] + 1995;
}

# write the first model prediction results.
meanA.write(file: 'mean-a.dat');
varA.write(file: 'var-a.dat');

# write the second model prediction results.
meanB.write(file: 'mean-b.dat');
varB.write(file: 'var-b.dat');

